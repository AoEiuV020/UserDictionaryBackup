apply plugin: 'com.android.application'

def localPropertiesFile = project.rootProject.file('local.properties')
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

android {
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        config {
            keyAlias localProperties['keyAlias']
            keyPassword localProperties['keyPassword']
            storeFile localProperties['storeFile'] != null ?
                    file('.signing/' + localProperties['storeFile']) :
                    null
            storePassword localProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "cc.aoeiuv020.userdictionarybackup"
        minSdkVersion 22
        targetSdkVersion 25
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (localProperties['storeFile'] != null)
                signingConfig signingConfigs.config
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (output.name == 'release' && variant.signingReady)
                        output.outputFile = new File(output.outputFile.parent,
                                "${project.rootProject.name}-${variant.versionName}.apk"
                        )
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:25.3.0'
}
